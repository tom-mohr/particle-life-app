import org.gradle.internal.os.OperatingSystem

plugins {
    id "java"
    id "application"                                     // for "run" task
    id 'edu.sc.seis.launch4j' version '3.0.6'            // for generating .exe
    id "io.github.goooler.shadow" version "8.1.8"        // only needed to make linux build
}

// use Java 21
def TARGET_JAVA = JavaLanguageVersion.of(21)
java {
    toolchain {
        languageVersion = TARGET_JAVA
    }
}

def buildDir = layout.buildDirectory.get().asFile
def workingDir = "$buildDir/app"

def javaHome = javaToolchains.launcherFor {
    languageVersion = TARGET_JAVA
}.get().metadata.installationPath

// creates folder "jre"
tasks.register('createRuntime') {
    printf("Using Java Home: %s%n", javaHome)
    def jlink = file("${javaHome}/bin/${OperatingSystem.current().isWindows() ? "jlink.exe" : "jlink"}")
    def outputDir = file("$buildDir/jre")

    inputs.file(jlink)
    outputs.dir(outputDir)

    doLast {
        outputDir.deleteDir()
        exec {
            commandLine jlink,
                    "--no-header-files",
                    "--no-man-pages",
                    "--strip-debug",
                    "--module-path", "${javaHome}/jmods",
                    "--compress", "zip-0",  // no compression here -> will be compressed in final zip
                    "--add-modules", "java.base,java.desktop,java.scripting,java.sql,jdk.unsupported",
                    "--output", outputDir
            /*
            Note: The set of modules above has been determined with jdep.
            1. Build the project: ./gradlew build
            2. Remove build/libs/particle-life-app.jar
            3. Run jdeps --print-module-deps --ignore-missing-deps --module-path build/libs build/libs/particle-life-app-all.jar
            The output lists all required modules in the last line.
             */
        }
    }
}

application {
    mainClass = "com.particle_life.app.Main"  // required for "run" task

    if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
        applicationDefaultJvmArgs = ["-XstartOnFirstThread"]
    }
}

// copy resources to build directory
tasks.register("copyResources", Copy) {
    from "$projectDir/src/main/resources"
    into workingDir
}

run {
    dependsOn(tasks.copyResources)
    delegate.workingDir = workingDir
}

launch4j {
    icon = "${projectDir}/favicon.ico"
    mainClassName = 'com.particle_life.app.Main'
    dontWrapJar = true
    requires64Bit = true
    bundledJrePath = "jre"
}

tasks.register('copyJre', Copy) {
    dependsOn tasks.createRuntime

    from "$buildDir/jre"
    into "$workingDir/jre"
}

tasks.register('copyLaunch4j', Copy) {
    dependsOn tasks.createExe

    from "$buildDir/launch4j"
    into workingDir
}

tasks.register('assembleApp') {
    dependsOn tasks.copyLaunch4j, tasks.copyResources, tasks.copyJre
}

tasks.register('zipApp', Zip) {
    dependsOn tasks.assembleApp
    from workingDir
    destinationDirectory = layout.buildDirectory.dir("zipApp")
    archiveFileName = "particle-life-app.zip"
}

group 'com.particle.life.app'

ext {
    lwjglVersion = "3.3.6"  // requires at least Java 21
    imGuiVersion = "1.86.2"
}

jar {
    // This ignores all resource files when bundling the JAR.
    // Reason: We don't want our "resources" files to be included
    // in the JAR, but instead outside, next to the executable.
    processResources.exclude('*')
}

// set imGuiNatives
switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        def osArch = System.getProperty("os.arch")
        project.ext.imGuiNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
                ? "linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
                : "linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.imGuiNatives = "macos"
        break
    case OperatingSystem.WINDOWS:
        def osArch = System.getProperty("os.arch")
        project.ext.imGuiNatives = osArch.contains("64")
                ? "windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                : "windows-x86"
        break
}

// set lwjglNatives (code from https://www.lwjgl.org/customize)
switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        def osArch = System.getProperty("os.arch")
        if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
            project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
        } else if (osArch.startsWith("ppc")) {
            project.ext.lwjglNatives += "-ppc64le"
        } else if (osArch.startsWith("riscv")) {
            project.ext.lwjglNatives += "-riscv64"
        }
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
}

repositories {
    mavenCentral()
    mavenLocal()

    // JitPack allows to easily add dependencies on GitHub repos
    // (e.g. implementation 'com.github.User:Repo:Tag')
    maven { url "https://jitpack.io" }  // this line should be at the end of the repositories
}

dependencies {

    // Particle Life Backend
    implementation "com.github.tom-mohr:particle-life:v0.5.2"

    // YAML Parser "YamlBeans"
    implementation 'com.esotericsoftware.yamlbeans:yamlbeans:1.17'

    // TOML Parser
    implementation 'com.moandjiezana.toml:toml4j:0.7.2'

    // Apache Commons Text (for Levenshtein distance)
    implementation 'org.apache.commons:commons-text:1.12.0'

    // Linear Algebra Library "JOML"
    implementation 'org.joml:joml:1.10.1'

    // GUI Library "Dear ImGui" from https://github.com/SpaiR/imgui-java
    implementation "io.github.spair:imgui-java-binding:$imGuiVersion"
    implementation "io.github.spair:imgui-java-natives-$imGuiNatives:$imGuiVersion"

    // Lightweight Java Game Library "LWJGL" (code from https://www.lwjgl.org/customize)
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"  // cleaner: replaced "implementation" with "runtimeOnly" for natives
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
}
